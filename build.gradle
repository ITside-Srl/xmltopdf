plugins {
  id 'java'
  id 'eclipse-wtp'
}

sourceCompatibility = 8
targetCompatibility = 8

version = "2.0.0.0"

repositories {
    mavenCentral()
}

ext {
	iTextVersion = '[9.0.0,)'
}

dependencies {
	implementation "com.itextpdf:io:${iTextVersion}"
	implementation "com.itextpdf:layout:${iTextVersion}"
	implementation "com.itextpdf:styled-xml-parser:${iTextVersion}"
	implementation "com.itextpdf:html2pdf:[6.0.0,)"
}

jar {
    manifest {
		attributes (
	    	"Implementation-Title" : project.name,
	    	"Implementation-Version": project.version,
	    	"Implementation-Vendor": "ITside Srl",
			'Main-Class': 'it.itside.xml2pdf.BatchConvertMain'
	    )
	}
}

def baseName = 'xml2pdf'

tasks.register("deleteLibs", Delete) {
    delete fileTree(dir: "${layout.buildDirectory.get()}/libs/"+"$baseName"+"_lib", include: '**.jar')
}

tasks.register("copyLibs", Copy) {
	//dependsOn deleteLibs
    into "${layout.buildDirectory.get()}/libs/"+"$baseName"+"_lib"
    from configurations.runtimeClasspath
}

tasks.register("customFatJar", Jar) {
	dependsOn copyLibs
	
    archiveBaseName = baseName
	archiveClassifier = ''
	archiveVersion = ''
	
    manifest {
        attributes 	(
        	"Implementation-Title" : project.name,
	    	"Implementation-Version": project.version,
	    	"Implementation-Vendor": "ITside Srl",
			'Main-Class': 'it.itside.xml2pdf.BatchConvertMain',
           	'Class-Path': configurations.runtimeClasspath.files.collect { "$baseName" + "_lib/$it.name" }.join(' ')
		)
    }
    
    with jar
}

